FILE(GLOB SRC *.cpp *.h *.def)
IF(APPLE)
  FILE(GLOB MAC_SRC mac/*.h mac/*.m mac/*.mm mac/*.cpp)
  SET(SRC ${SRC} ${MAC_SRC})
  SET_SOURCE_FILES_PROPERTIES(${SRC}
    PROPERTIES COMPILE_FLAGS "-x objective-c++")
ENDIF(APPLE)

IF(WIN32)
  SET(NLDRV_OGL_LIB "nel_drv_opengl_win")
ELSE(WIN32)
  SET(NLDRV_OGL_LIB "nel_drv_opengl")
ENDIF(WIN32)

NL_TARGET_DRIVER(${NLDRV_OGL_LIB} ${SRC})

INCLUDE_DIRECTORIES(${OPENGL_INCLUDE_DIR})

IF(WITH_STLPORT)
  TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${CMAKE_THREAD_LIBS_INIT})
ENDIF(WITH_STLPORT)

TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} nel3d nelmisc ${OPENGL_gl_LIBRARY})
NL_DEFAULT_PROPS(${NLDRV_OGL_LIB} "NeL, Driver, Video: OpenGL")
NL_ADD_LIB_SUFFIX(${NLDRV_OGL_LIB})
NL_ADD_RUNTIME_FLAGS(${NLDRV_OGL_LIB})

IF(WIN32)
  INCLUDE_DIRECTORIES(${DXSDK_INCLUDE_DIR})
  TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${DXSDK_DINPUT_LIBRARY} ${DXSDK_GUID_LIBRARY})
  ADD_DEFINITIONS(/DDRIVER_OPENGL_EXPORTS)
ENDIF(WIN32)

IF(APPLE)
  TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${CARBON})
  TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${COCOA})
ENDIF(APPLE)

IF(UNIX AND NOT APPLE)
  TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${X11_X11_LIB})
  IF(XF86VidMode_FOUND)
    INCLUDE_DIRECTORIES(${XF86VidMode_INCLUDE_DIR})
    ADD_DEFINITIONS(${XF86VidMode_DEFINITIONS})
    TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${XF86VidMode_LIBRARY})
  ENDIF(XF86VidMode_FOUND)
  IF(X11_Xrandr_FOUND)
    INCLUDE_DIRECTORIES(${X11_Xrandr_INCLUDE_PATH})
    ADD_DEFINITIONS(-DHAVE_XRANDR)
    TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${X11_Xrandr_LIB})
  ENDIF(X11_Xrandr_FOUND)
  IF(X11_Xrender_FOUND)
    INCLUDE_DIRECTORIES(${X11_Xrender_INCLUDE_PATH})
    ADD_DEFINITIONS(-DHAVE_XRENDER)
    TARGET_LINK_LIBRARIES(${NLDRV_OGL_LIB} ${X11_Xrender_LIB})
  ENDIF(X11_Xrender_FOUND)
ENDIF(UNIX AND NOT APPLE)

IF(NOT APPLE AND WITH_PCH)
  ADD_NATIVE_PRECOMPILED_HEADER(${NLDRV_OGL_LIB} ${CMAKE_CURRENT_SOURCE_DIR}/stdopengl.h ${CMAKE_CURRENT_SOURCE_DIR}/stdopengl.cpp)
ENDIF(NOT APPLE AND WITH_PCH)

INSTALL(TARGETS ${NLDRV_OGL_LIB} LIBRARY DESTINATION ${NL_DRIVER_PREFIX} ARCHIVE DESTINATION lib RUNTIME DESTINATION bin COMPONENT drivers3d)
IF(WITH_MAXPLUGIN)
  INSTALL(TARGETS ${NLDRV_OGL_LIB} RUNTIME DESTINATION maxplugin COMPONENT drivers3d)
ENDIF(WITH_MAXPLUGIN)
